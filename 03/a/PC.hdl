// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=outpre, out=o1);
    Mux16(a=outpre, b=o1, sel=inc, out=i1);
    Mux16(a=i1, b=in, sel=load, out=i2);
    Mux16(a=i2, b=false, sel=reset, out=ri);
    Or(a=reset, b=load, out=load0);
    Or(a=load0, b=inc, out=load1);
    Register(in=ri, load=load1, out=out, out=outpre);
}
